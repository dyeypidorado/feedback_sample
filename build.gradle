apply plugin: 'com.android.application'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
    }
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.1"

    defaultConfig {
        versionCode 1
        versionName "1.0"
        minSdkVersion 17
        targetSdkVersion 19
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
    productFlavors {
        dev {
            applicationId = "com.intelimina.feedbacksample"
            buildConfigField "String", "SERVER_URL", "\"http://" + getLocalIp("en0") + ":3000/\""
            buildConfigField "String", "FEEDBACK_PATH", "\"/api/v1/feedbacks.json\""
            buildConfigField "String", "FEEDBACK_TOKEN", "\"2HsD41-3spBoC_ybeu-Y\""
        }

        staging {
            applicationId = "com.intelimina.feedbacksample"
            buildConfigField "String", "SERVER_URL", "\"http://biofemme-dev.intelimina.com\""
            buildConfigField "String", "FEEDBACK_PATH", "\"/api/v1/feedbacks.json\""
            buildConfigField "String", "FEEDBACK_TOKEN", "\"MYSAMPLE_TOKEN\""
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:19.1.0'
}

// http://bartinger.at/inject-dynamic-host-ip-address-with-gradle/
def getLocalIp(String interfaceName) {
    NetworkInterface iface = NetworkInterface.getByName(interfaceName);
    for (InterfaceAddress address : iface.getInterfaceAddresses()) {
        String ip = address.getAddress().getHostAddress()
        if (ip.length() <= 15) {
            return ip;
        }
    }
}